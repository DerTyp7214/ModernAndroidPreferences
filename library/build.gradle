apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

// Version
def library_version = '0.4.2'
def library_name = 'modernpreferences'
def library_name_bintray = 'ModernAndroidPreferences'

android {
    compileSdkVersion 28
    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 1
        versionName library_version
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    lintOptions {
        abortOnError false
    }
    androidExtensions {
        experimental = true
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation "androidx.appcompat:appcompat:1.1.0"
    implementation "androidx.constraintlayout:constraintlayout:1.1.3"
    implementation "androidx.recyclerview:recyclerview:1.1.0"

    implementation "androidx.core:core-ktx:1.2.0"
}

task sourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from android.sourceSets.main.java.srcDirs
}

def bintrayKey = null
def githubToken = null
def propFile = project.file("key.properties")
if (propFile.exists()) {
    Properties props = new Properties()
    props.load(new FileInputStream(propFile))
    bintrayKey = props["bintrayKey"]
    githubToken = props["githubToken"]
}

// Maven & Bintray config
publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/Maxr1998/ModernAndroidPreferences")
            credentials {
                username = "Maxr1998"
                password = githubToken
            }
        }
    }
    publications {
        Production(MavenPublication) {
            groupId 'de.Maxr1998.android'
            artifactId library_name
            version library_version
            artifact("$buildDir/outputs/aar/library-release.aar")
            artifact sourcesJar

            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')
                configurations.implementation.allDependencies.each {
                    def dependencyNode = dependenciesNode.appendNode('dependency')
                    dependencyNode.appendNode('groupId', it.group)
                    dependencyNode.appendNode('artifactId', it.name)
                    dependencyNode.appendNode('version', it.version)
                }
            }
        }
    }
}

bintray {
    user = 'maxr1998'
    key = bintrayKey
    publications = ['Production']
    pkg {
        repo = 'maven'
        name = library_name_bintray
        version {
            name = library_version
        }
        publish = true
    }
}